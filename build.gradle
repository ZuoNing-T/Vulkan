import org.gradle.internal.os.OperatingSystem

apply plugin: 'java'
apply plugin: 'kotlin'

// jitpack
apply plugin: 'maven'
group = 'com.github.kotlin-graphics'

apply plugin: 'com.github.johnrengelman.shadow'

kotlin {
    experimental {
        coroutines 'enable'
    }
}

//compileKotlin {
//    kotlinOptions {
//        jvmTarget = '1.8'
//    }
//}

allprojects {

    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    apply plugin: 'kotlin'
    compileKotlin {
        kotlinOptions.jvmTarget = '1.8'
    }
}

buildscript {

    ext.kotlinVersion = '1.2.50'

    repositories {
        jcenter()   // shadow
        mavenCentral()
        maven { url = 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { setUrl("https://oss.sonatype.org/content/repositories/snapshots/") }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2'
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    ext.kx = 'com.github.kotlin-graphics'
    implementation "$kx:glm:b12f1818240ee21bd29b7f8a70fb45e23c9e5aff"
    implementation "$kx:vkk:42327f5bd47c766baa922678ab497efc77fbdc98"
    implementation "$kx:uno-sdk:1573c122ad690d85eece5542e2fec08db4de3c1e"
    implementation "$kx:assimp:598051180e5ca467f5c9b806653b21f3779c07f7"
//    implementation "$kx:imgui:6aea5d675ec24b45ffda10e9106eb47656c049b2"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:0.21"

    testCompile 'io.kotlintest:kotlintest:2.0.7'

    ext.lwjglVersion = "3.1.7-SNAPSHOT"
    switch (OperatingSystem.current()) {
        case OperatingSystem.WINDOWS:
            ext.lwjglNatives = "natives-windows"
            break
        case OperatingSystem.LINUX:
            ext.lwjglNatives = "natives-linux"
            break
        case OperatingSystem.MAC_OS:
            ext.lwjglNatives = "natives-macos"
            break
    }
    ["", "-glfw", "-jemalloc", "-openal", "-opengl", "-stb", "-vulkan"].each {
        implementation "org.lwjgl:lwjgl$it:$lwjglVersion"
        runtime "org.lwjgl:lwjgl$it:$lwjglVersion:$lwjglNatives"
    }

    ext.spirvCross = "graphics.scenery:spirvcrossj:0.4.0"
    implementation spirvCross
    runtime "$spirvCross:$lwjglNatives"
}

repositories {
    mavenCentral()
    maven { setUrl("https://dl.bintray.com/kotlin/kotlin-eap") }
    maven { setUrl("https://oss.sonatype.org/content/repositories/snapshots/") }
    maven { setUrl('https://jitpack.io') }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}